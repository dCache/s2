% FileNames00: test length of filename
% This test checks if filenames of 190 characters are allowed in Put operations.
% After putting the file the test lists it and then deletes it again.
%
% SRM calls performed by the test:
% --------------------------------
% srmPrepareToPut
% srmStatusOfPutRequest
% srmPutDone
% srmLs
% srmRm

% Puts a file of length 255 characters into the SRM, then lists it and then deletes it again.

TEST $MATCH{(?P<srmTest>.*).s2$ ${0}}
   #include ../include/PutCycle.s2
   #include ../include/GetCycle.s2


&& SYSTEM echo "${srmTest}: Putting file" >> "$ENV{S2_LOG}"

&& FUN PutCycle "$ENV{FILE_TO_PUT0}" "$ENV{SRM_LONG_PATH190}" : putRequestToken

&& NOP
   srmStatusOfPutRequest $ENV{ENDPOINT} requestToken=${putRequestToken} SURL[$ENV{SRM_LONG_PATH190}] fileStatuses=(?P<FileStatus0>.*) remainingTotalRequestTime=(?P<remainingTotalRequestTime>.*) returnStatus.explanation=(?P<returnStatus_explanation>.*) returnStatus.statusCode=(?P<returnStatus_statusCode>.*)
   && TEST $MATCH{SRM_NOT_SUPPORTED ${returnStatus_statusCode}}
      SYSTEM echo "${srmTest}: Filenames 255 characters-long are not supported" >> "$ENV{SRM2_SUPPORTED}"
      && ASSIGN Status 0
   || SYSTEM exit 0
   && TEST $MATCH{".*returnStatus.statusCode0=(?P<StatusCode0>.+?)(\s.+|$)" ${FileStatus0}}
      TEST $MATCH{SRM_FAILURE ${returnStatus_statusCode}} && TEST $MATCH{SRM_INVALID_PATH ${StatusCode0}}
         SYSTEM echo "${srmTest}: Filenames 255 characters-long are not supported" >> "$ENV{SRM2_SUPPORTED}"
         && ASSIGN Status 0
   || NOP
      TEST $MATCH{SRM_SUCCESS ${returnStatus_statusCode}}
         ASSIGN Status 0
      || TEST $DEFINED{Status}
         SYSTEM exit 0

&& SYSTEM echo "${srmTest}: Listing file" >> "$ENV{S2_LOG}"

&& NOP
   srmLs $ENV{ENDPOINT} SURL[$ENV{SRM_LONG_PATH190}] fullDetailedList=1 requestToken=(?P<requestLsToken>.*) pathDetails=".*path0=(?P<Path0>.+?)(\s.+|$)" returnStatus.explanation=(?P<returnStatus_explanation>.*) returnStatus.statusCode=(?P<returnStatus_statusCode>.*)
      SYSTEM echo "${srmTest}: Executed srmLs..." >> "$ENV{S2_LOG}"
      && SYSTEM echo "${srmTest}: returnStatus=${-returnStatus_statusCode}, SURL=${Path0}" >> "$ENV{S2_LOG}"
   || SYSTEM echo "${srmTest}: KO, Ls of file not supported" >> "$ENV{SRM2_SUPPORTED}" && exit ${!}

&& SYSTEM echo "${srmTest}: OK, Filenames with 255 characters are supported" >> "$ENV{SRM2_SUPPORTED}"
|| ASSIGN Status ${!}
   SYSTEM echo "${srmTest}: KO ${-returnStatus_explanation}" >> "$ENV{SRM2_SUPPORTED}" && exit ${Status}

;; SYSTEM echo "${srmTest}: Deleting the file again" >> "$ENV{S2_LOG}"

;; srmRm $ENV{ENDPOINT} SURL[$ENV{SRM_LONG_PATH190}] fileStatuses=(?P<fileStatuses>.*) returnStatus.explanation=(?P<returnStatus_explanation>.*) returnStatus.statusCode=(?P<returnStatus_statusCode>.*)

;; SYSTEM exit ${Status}
