% Pin01: Put a file, then increase the pin lifetime using the request token and the SURL, which must not work
%
% put cycle:
%   srmPrepareToPut
%   srmStatusOfPutRequest
%   srmPutDone
% srmExtendFileLifeTime
%
% From the notes on the behaviour of: srmExtendFileLifeTime
% This method allows to change only one lifetime at a time (either SURL lifetime by the newFileLifetime or pin
% lifetime by the newPinLifetime), depending on the presence or absence of the request token. SURL lifetimes are
% on SURLs that resulted from the successful srmCopy or srmPrepareToPut followed by srmPutDone, and pin
% lifetimes are on TURLs or file copies that resulted from srmPrepareToGet, srmPrepareToPut or srmBringOnline.

#include ../include/PutCycle.s2
#include ../include/GetCycle.s2
;; TEST $MATCH{(?P<srmTest>.*).s2$ ${0}}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
&& SYSTEM echo "${srmTest}: Executing Put cycle on $ENV{SRM_NEW_FILE0}" >> "$ENV{S2_LOG}"
&& ASSIGN fileToPut $ENV{FILE_TO_PUT0}
&& ASSIGN sURL $ENV{SRM_NEW_FILE0}
   FUN PutCycle ${fileToPut} ${sURL} : putRequestToken

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% use srmExtendFileLifeTime to try to extend the pin time for the TURL

&& SYSTEM echo "${srmTest}: Executing ExtendFileLifeTime to extend pintime of TURL/requestToken must be specified" >> "$ENV{S2_LOG}"
&& SYSTEM echo "${srmTest}: This operation should fail and SRM_INVALID_REQUEST should be returned at file level" >> "$ENV{S2_LOG}"

&& srmExtendFileLifeTime $ENV{ENDPOINT} requestToken=${putRequestToken} SURL[${sURL}] newPinLifeTime=$ENV{PIN_LIFETIME} fileStatuses=".*returnStatus.statusCode0=(?P<FileStatusCode0>.*?)( |$)" returnStatus.explanation=(?P<returnStatus_explanation>.*) returnStatus.statusCode=(?P<returnExtendTURL_statusCode>.*)
   TEST $MATCH{(SRM_INVALID_REQUEST|SRM_FAILURE) ${FileStatusCode0}}
      SYSTEM echo "${srmTest}: OK" >> "$ENV{SRM2_SUPPORTED}"
      && ASSIGN Status 0
   || TEST $MATCH{SRM_SUCCESS ${FileStatusCode0}}
      SYSTEM echo "${srmTest}: KO srmExtendFileLifeTime on TURL succeeded after a PutCycle" >> "$ENV{SRM2_SUPPORTED}"
      && ASSIGN Status 2

|| ASSIGN Status ${!}
   SYSTEM echo "${srmTest}: KO" >> "$ENV{SRM2_SUPPORTED}"

;; srmRm $ENV{ENDPOINT} SURL[$ENV{SRM_NEW_FILE0}] fileStatuses=(?P<filestatuses>.*) returnStatus.explanation=(?P<returnStatus_explanation>.*) returnStatus.statusCode=(?P<returnStatus_statusCode>.*)
;; SYSTEM exit ${Status}
