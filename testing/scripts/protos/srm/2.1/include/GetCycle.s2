; GetCycle with one file
;
; @param: ${fileToGet}
; @param: ${sURL}
;
; srmPrepareToGet
; srmStatusOfGetRequest

#ifndef _GET_CYCLE_
#define _GET_CYCLE_

; Generic functions
#include ../../../../include/Basic.s2
; SRM functions
#include ../../include/Basic.s2
#include ../../include/FileTransGet.s2

DEFUN DefFileStatuses f : t
   ASSIGN t ".*returnStatus.statusCode${1c}=(SRM_SUCCESS|SRM_DONE|SRM_FILE_IN_CACHE) .*transferURL${1c}=(?P<transferURL${1c}>.*?)( |$)"
   && ASSIGN 1c $EXPR{${1c}+1}

DEFUN GetCycle filesToGet sURLs : getRequestToken
   ; Define local variables
   ASSIGN storageTypes $MAP{StorageTypes $SPLIT{${filesToGet}}}
   && ASSIGN c 0 transferURLs ""
   && ASSIGN fileStatuses "$MAP{DefFileStatuses $SPLIT{${filesToGet}}}"
   
   && srmPrepareToGet $ENV{ENDPOINT} userID=$ENV{USER} arrayOfFileRequests.fileStorageType[$SPLIT{${storageTypes}}] arrayOfFileRequests.SURLOrStFN[$SPLIT{${sURLs}}] arrayOfTransferProtocols[$ENV{TRANS_PROT0} $ENV{TRANS_PROT1}] requestToken=(?P<getRequestToken>.*) fileStatuses="(?P<ptgFileStatuses>.*)" returnStatus.explanation=(?P<rs_explanation>.*) returnStatus.statusCode=(SRM_SUCCESS|SRM_DONE|SRM_REQUEST_QUEUED)
      SYSTEM echo "srmPrepareToGet: getRequestToken=${getRequestToken}" >> $ENV{S2_LOG}
      && >1||3 SLEEP $ENV{SLEEP_SOR}
         srmStatusOfGetRequest $ENV{ENDPOINT} userID=$ENV{USER} requestToken=${getRequestToken} surlArray[$SPLIT{${sURLs}}] fileStatuses="${fileStatuses}" returnStatus.explanation=(?P<rs_explanation>.*) returnStatus.statusCode=(SRM_SUCCESS|SRM_DONE)
         || SYSTEM echo "srmStatusOfGetRequest failed" >> $ENV{S2_LOG} && exit 2

      ; The actual physical transfer
      && ASSIGN c 0
         WHILE FUN car ${filesToGet} : fileToGet filesToGet
            FUN FileTransGet ${fileToGet} ${transferURL${c}}
            && ASSIGN c $EXPR{${c}+1}
         || TEST "${filesToGet}" == "" && !$DEFINED{transferURL${c}}

   && SYSTEM echo "Get cycle succeeded" >> $ENV{S2_LOG}

; _GET_CYCLE_
#endif
